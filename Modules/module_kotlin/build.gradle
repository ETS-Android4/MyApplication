plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    //kotlin('android')
    //kotlin('kapt')
    id 'com.google.protobuf'
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    //自定义源文件
    sourceSets {
        //main.java.srcDirs += 'src/main/kotlin'
        main {
            proto {
                srcDir 'src/main/proto'
            }
            java {
                srcDir 'src/main/kotlin'
            }
        }
    }
    //gradle:4.1.0+
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    resourcePrefix "kotlin_"
}

dependencies {
    implementation project(path: ':module')
    //Android
    implementation Deps.appcompat
    implementation Deps.constraintlayout
    //Gson
    implementation Deps.gson
    //ARouter
    implementation Deps.arouter
    kapt Deps.arouter_compiler
    //Kotlin
    implementation Deps.kotlin
    implementation Deps.core_ktx
    //Coroutines
    implementation Deps.kotlin_coroutines
    implementation Deps.livedata_ktx//LiveDataScope
    implementation Deps.viewmodel_ktx//ViewModelScope
    //Proto DataStore
    implementation Deps.datastore
    //protobuf
    //You need to depend on the lite runtime library, not protobuf-java
    implementation 'com.google.protobuf:protobuf-javalite:3.10.0'
    //Preferences DataStore
    implementation Deps.datastore_pre
    //Paging3
    implementation Deps.paging
    implementation Deps.paging_rxjava
    //RxJava
    implementation Deps.rxandroid
    //Retrofit
    implementation Deps.retrofit
    implementation Deps.retrofit_gson
    implementation Deps.retrofit_rxjava
}

//https://github.com/google/protobuf-gradle-plugin
protobuf {
    // 配置protoc可执行文件
    // Configure the protoc executable
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}
